Start =
	__
    data:
    (
    	"section"i _ ".data"i  Comment? __
    	data:(data:DataLine { return { line: data, location: location() }; })*
    	{ return data; }
    )?
    __
    text:
    (
    	"section"i _ ".text"i Comment? __
        text:(text:TextLine { return { line: text, location: location() }; })*
        { return text; }
    )?
    { return { data: data !== null ? data : [], text: text !== null ? text : []}; }

EmptyLine =
	Comment / _

Comment =
	_ ";" [^\n]*

DataLine =
	(label:Label? _ size:DataSize _ constant:DataConstant constants:(_ "," _ constant:DataConstant { return constant; })* Comment? __
    {
    	return { label: label, size: size, constants: [constant].concat(constants) };
    }) /
    (label:Label Comment? __ { return { label: label, size: null, constants: [] }; })

DataConstant =
	Number / String

DataSize =
	"db" { return 1; } /
    "dw" { return 2; } /
    "dd" { return 4; }

TextLine =
	(_ label:Label? _ inst:Instruction Comment? __ { return { tag: "Line", label: label, instruction: inst }; }) /
    (_ label:Label Comment? __ { return { tag: "Line", label: label, instruction: null }; })

Label =
	local:"."? name:Identifier ":" { return { tag: "Label", name: name, local: local !== null }; }

Instruction =
	NoOperandInstruction / JumpInstruction / SingleOperandInstruction /
    MemoryInstruction / ArithmeticInstruction / CompareInstruction

NoOperandInstruction =
	("CLD" / "STD" / "CLC" / "STC" / "ENTER" / "RET" / "LEAVE" / "HLT")
	{ return { tag: "Instruction", type: "NoOperand", name: text() }; }

JumpInstruction =
	name:$(
    	"JMP" / "JE" / "JNE" / "JZ" / "JNZ" / "JB" / "JNAE" / "JC" /
    	"JNB" / "JAE" / "JNC" / "JBE" / "JNA" / "JA" / "JNBE" / "JL" /
        "JNGE" / "JGE" / "JNL" / "JLE" / "JNG" / "JG" / "JNLE" / "JCXZ" /
        "JECXZ" / "JP" / "JPE" / "JNP" / "JPO" / "JO" / "JNO" / "JS" /
        "JNS" / "CALL" / "LOOP"
    ) _ target:ReadableExpression
    { return { tag: "Instruction", type: "Jump", name: name, operands: [target] }; }

SingleOperandInstruction =
	name:$(
    	"POP" / "INC" / "DEC"
    ) _ target:WritableExpression { return { tag: "Instruction", type: "SingleOperand", name: name, operands: [target] }; } /
    name:$(
    	"PUSH" / "INT"
    ) _ target:ReadableExpression { return { tag: "Instruction", type: "SingleOperand", name: name, operands: [target] }; }

MemoryInstruction =
	name:$("MOV" / "MOVSX") _ target:WritableExpression _ "," _ source:ReadableExpression
    { return { tag: "Instruction", type: "Memory", name: name, operands: [target, source] }; }

ArithmeticInstruction =
	(
	    name:$("ADD" / "SUB" / "ADC" / "SBB") _ target:WritableExpression "," _ source:ReadableExpression
        { return { tag: "Instruction", type: "Arithmetic", name: name, operands: [target, source] }; }
    ) /
    (
        name:$("MUL" / "IMUL" / "DIV" / "IDIV") _ source:ReadableExpression
        { return { tag: "Instruction", type: "Arithmetic", name: name, operands: [source] }; }
    )

CompareInstruction =
    name:$("CMP") _ op1:ReadableExpression _ "," _ op2:ReadableExpression
    { return { tag: "Instruction", type: "Compare", name: name, operands: [op1, op2] }; }

WritableExpression =
	Register /
    cast:SizeCast _ value:RegisterDereferenceExpression { return { tag: "Cast", size: cast, value: value }; } /
    cast:SizeCast _ value:AddressDereferenceExpression { return { tag: "Cast", size: cast, value: value }; }

ReadableExpression =
	Register /
    cast:SizeCast _ value:RegisterDereferenceExpression { return { tag: "Cast", size: cast, value: value }; } /
    cast:SizeCast _ value:AddressDereferenceExpression { return { tag: "Cast", size: cast, value: value }; } /
    cast:SizeCast _ value:LabelExpression { return { tag: "Cast", size: cast, value: value }; } /
    cast:SizeCast _ value:Number { return { tag: "Cast", size: cast, value: value }; }

AddressDereferenceExpression =
	"[" _ label:LabelExpression _ "]" { return { tag: "Label", value: label.value, deref: true }; } /
    "[" _ number:Number _ "]" { return { tag: "Number", value: number.value, deref: true }; }

LabelExpression =
	local:(".")? target:Identifier { return { tag: "Label", value: local !== null ? ("." + target.value) : target.value }; }

RegisterDereferenceExpression =
	(("[" _ baseReg:Register _ indexer:IndexExpression? _ "]")
    {
    	return {
    		tag: "Mem",
        	baseRegister: baseReg,
        	index: indexer !== null ? indexer.index : null,
        	constant: indexer !== null && indexer.constant !== null ? indexer.constant : null
    	};
    })

IndexExpression =
	(
        index:("+" _ register:Register _ "*" _ multiplier:Number
        	{ return { register: register, multiplier: multiplier }; })?
        _
        constant: (operator:$("+" / "-") _ constant:Number
                    {
                    	if (operator === "-")
                    	{
                    		constant.value *= -1;
                    	}
                    	return constant;
                    } )?
    ) { return { index: index, constant: constant }; }

Register =
	(
    	"EAX" / "AX" / "AH" / "AL" / "EBX" / "BX" / "BH" / "BL" /
    	"ECX" / "CX" / "CH" / "CL" / "EDX" / "DX" / "DH" / "DL" /
    	"EBP" / "ESP" / "EDI" / "ESI"
    ) { return { tag: "Reg", name: text() }; }

SizeCast =
	"BYTE" { return 1; } /
    "WORD" { return 2; } /
    "DWORD" { return 4; } /
    "" { return 0; }

Identifier =
	[a-zA-Z_][a-zA-Z_0-9]* { return { tag: "Identifier", value: text() }; }

Number =
	"0x"[0-9a-fA-F]+ { return { tag: "Number", value: parseInt(text(), 16) }; } /
	[0-9]+ { return { tag: "Number", value: parseInt(text(), 10) }; }

String =
	"'" value:("\\'" { return "'"; } / [^'])* "'" { return { tag: "String", value: value }; }

_ "whitespace"
  = [ \t\r]*

__ "whitespace_plus_lines"
  = [ \t\r\n]*
